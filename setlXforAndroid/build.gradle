apply plugin: 'com.android.application'

repositories {
    mavenLocal()
}

dependencies {
    compile 'org.randoom.setlx:setlX-core:2.5.0-SNAPSHOT'
    compile project(':android-filechooser')
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        disable 'InvalidPackage','GoogleAppIndexingWarning','RtlHardcoded'
    }
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn generateVersionJava
}

task generateVersionJava {
    doLast {
        def script = "bash setlXforAndroid/generateVersionJava.sh".execute()
        script.in.eachLine {line -> println line}
        script.err.eachLine {line -> println "ERROR: " + line}
        script.waitFor()
    }
}

task copyDeps(type: Copy) {
  from configurations.compile
  into 'build/libraries'
}

generateVersionJava.dependsOn(copyDeps)

